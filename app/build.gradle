apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

ext {
    minimumSdkVersion = 26
    versionMajor = 1
    versionMinor = 1
    versionPatch = 2
}

private Integer generateVersionCode() {
    return project.ext.minimumSdkVersion * 1000000 + project.ext.versionMajor * 10000 + project.ext.versionMinor * 100 + project.ext.versionPatch
}

private String generateVersionName() {
    return "${project.ext.versionMajor}.${project.ext.versionMinor}.${project.ext.versionPatch}"
}

private void checkVersionCodeAndVersionName(Integer versionCode, String versionName) {
    Integer expectedVersionCode = generateVersionCode()
    if (versionCode != expectedVersionCode) {
        throw new GradleException("${versionCode} is not equal to ${expectedVersionCode}!")
    }
    String expectedVersionName = generateVersionName()
    if (versionName != generateVersionName()) {
        throw new GradleException("${versionName} is not equal to ${expectedVersionName}!")
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.github.fi3te.notificationcron"
        minSdkVersion 26
        targetSdkVersion 28
        // because of F-Droid it is not possible to use method calls for the versionCode and versionName
        versionCode 26010102
        versionName "1.1.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        checkVersionCodeAndVersionName(versionCode, versionName)

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.room:room-runtime:2.2.5'
    kapt 'androidx.room:room-compiler:2.2.5'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.cronutils:cron-utils:9.0.2'
    implementation 'com.afollestad.material-dialogs:core:3.1.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.truth:truth:1.0'
}
